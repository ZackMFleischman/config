" 
" File: .vimrc
" Author: Zack Fleischman
"

"------------------------
" My Leader Key is Space!
"------------------------
" Remap leader to the space bar. Do this before sourcing any mappings
let mapleader="\<Space>"

"-----------------------
" Sourcing External Files
"-----------------------
" Plugins 
source ~/.vimplugins
au BufReadPost .vimplugins set syntax=sh

"---------------------------
" Color Scheme
"---------------------------
colorscheme wombat256mod

"---------------------------
" Python specific stuff
"---------------------------
" Turn off smart indent for Python (# comments get messed up)
au! FileType python setl nosmartindent
set encoding=utf-8

" Snap to def and classes
nnoremap <C-j> $/\s*def.*(.*)\s*:\\|\s*class.*(.*)\s*:<CR>9kzt9j:nohlsearch<CR>f(b
nnoremap <C-k> 0?\s*def.*(.*)\s*:\\|\s*class.*(.*)\s*:<CR>9kzt9j:nohlsearch<CR>f(b
nnoremap <C-h> 0?\s*class.*(.*)\s*:<CR>9kzt9j:nohlsearch<CR>f(b
nnoremap <C-l> $/\s*class.*(.*)\s*:<CR>9kzt9j:nohlsearch<CR>f(b
noremap <leader>o 0?\s*def.*(.*)\s*:\\|\s*class.*(.*)\s*:<CR>9kzt9j:nohlsearch<CR><C-o>
noremap <leader>O 0?\s*class.*(.*)\s*:<CR>9kzt9j:nohlsearch<CR><C-o>

"================
" Custom Settings
"================

" So we don't have to reach for escape to leave insert mode.
inoremap jk <esc>

" Switch go to beginning of line / end of whitespace
nnoremap 0 ^
nnoremap ^ 0

" Remove trailing whitespace
nnoremap <leader>W :%s/\s\+$//<CR>
autocmd BufWritePre *.py :%s/\s\+$//e
autocmd BufWritePre *.html :%s/\s\+$//e
autocmd BufWritePre *.css :%s/\s\+$//e

" Navigate back a file
noremap <leader>` <C-^>

" 'Enter' is jump to line
nnoremap <CR> G
nnoremap <leader><CR> <CR>
:autocmd BufReadPost quickfix nnoremap <buffer> <CR> <CR>

" Jump back and forward
nnoremap <TAB> <C-o>
nnoremap <S-TAB> <TAB>

" Print filename
nnoremap <leader>L :echo expand('%:p')<CR>


"------
" Marks
"------
" Set Mark
noremap M m

" Switch mark jumpers
nnoremap ' `
nnoremap ` '


"----------------
" Persistent Undo
"----------------
set undodir=~/.vim/undodir
set undofile
set undolevels=1000 "maximum number of changes that can be undone
set undoreload=10000 "maximum number lines to save for undo on a buffer reload

"------------
" Tabs
"------------
" Goto Last Tab
let g:lasttab = tabpagenr()
au TabLeave * let g:lasttab = tabpagenr()
noremap <leader><TAB> :exe "tabn ".g:lasttab<CR>

" Goto Tab Number
noremap <leader>1 1gt
noremap <leader>2 2gt
noremap <leader>3 3gt
noremap <leader>4 4gt
noremap <leader>5 5gt
noremap <leader>6 6gt
noremap <leader>7 7gt
noremap <leader>8 8gt
noremap <leader>9 9gt

" Open in tab 
map <F6> :tabedit 

" Nav to next/prev tab
map <F8> :tabn<CR>  " Next Tab
map <F7> :tabp<CR>  " Previous Tab

" Move tabs around (These are mapped at the bottom)
function! ShiftTabRight()
    let currTabNum = tabpagenr()
    let maxTabNum = tabpagenr('$')
    if currTabNum == maxTabNum
        exe "tabm 0"
    else
        exe "tabm ".currTabNum
    endif
endfunction
function! ShiftTabLeft()
    let currTabNum = tabpagenr()
    let maxTabNum = tabpagenr('$')
    let next = currTabNum-2
    if currTabNum == 1
        exe "tabm ".maxTabNum
    else
        exe "tabm ".next
    endif
endfunction

" Set Tab Labels
if exists("+showtabline")
    function! MyTabLine()
        let s = ''
        let wn = ''
        let t = tabpagenr()
        let i = 1
        while i <= tabpagenr('$')
            let buflist = tabpagebuflist(i)
            let winnr = tabpagewinnr(i)
            let s .= '%' . i . 'T'
            let s .= (i == t ? '%1*' : '%2*')
            let s .= ' '
            let wn = tabpagewinnr(i,'$')

            let s .= '%#TabNum#'
            let s .= i
            let s .= (i == t ? '%#TabLineSel#' : '%#TabLine#')
            let bufnr = buflist[winnr - 1]
            let file = bufname(bufnr)
            let buftype = getbufvar(bufnr, 'buftype')
            if buftype == 'nofile'
                if file =~ '\/.'
                    let file = substitute(file, '.*\/\ze.', '', '')
                endif
            else
                let file = fnamemodify(file, ':p:t')
            endif
            if file == ''
                let file = '[No Name]'
            endif
            let s.= ' ' . file . ' '
            let i = i + 1
        endwhile
        let s .= '%T%#TabLineFill#%='
        let s .= (tabpagenr('$') > 1 ? '%999XX' : 'X')
        return s
    endfunction
    set stal=2
    set tabline=%!MyTabLine()
    set showtabline=1
    highlight link TabNum Special
endif

"-----------------------------
" Code Folding 
"-----------------------------

" Key Maps
nnoremap zj :set foldlevel=0<CR>zz
nnoremap zk :set foldlevel=1<CR>zz
nnoremap zl :set foldlevel=99<CR>zz

" Unfold everything on window open
:autocmd BufWinEnter * let &foldlevel = max(map(range(1, line('$')), 'foldlevel(v:val)'))

" Folded Text Colors
hi Folded ctermfg=18 ctermbg=243

" Custom Fold Text
fu! CustomFoldText()
    " Get first comment line
    let fs = v:foldstart
    while getline(fs) =~ '^\s*$' | let fs = nextnonblank(fs + 1)
    endwhile
    while getline(fs) =~ '^\s*"""\s*$' | let fs = nextnonblank(fs + 1)
    endwhile
    "while getline(fs) =~ '\"\"\"$' | let fs = nextnonblank(fs + 1)
    if fs > v:foldend
        let line = getline(v:foldstart)
    else
        let line = substitute(getline(fs), '\t', repeat(' ', &tabstop), 'g')
    endif

    "let w = winwidth(0) - &foldcolumn - (&number ? 8 : 0)
    let w = 80
    let foldSize = 1 + v:foldend - v:foldstart
    let foldSizeStr = " " . foldSize . " lines "
    let foldLevelStr = repeat("+--", v:foldlevel)
    let lineCount = line("$")
    let foldPercentage = printf("[%.1f", (foldSize*1.0)/lineCount*100) . "%] "
    let expansionString = repeat(".", w - strwidth(foldSizeStr.line.foldLevelStr.foldPercentage))
    return line . expansionString . foldSizeStr . foldPercentage . foldLevelStr
endf
set foldtext=CustomFoldText()

" Code folding columns on the left to see folds
"set foldcolumn=1

" Close folds automatically
"set foldclose=all

" Fold Method
set foldmethod=indent

"------------
" Indentation
"------------
nnoremap < <<
nnoremap > >>
vnoremap < <gv
vnoremap > >gv


"-----------
" Paste Mode
"-----------
nnoremap <leader>} :set paste<CR>i
nnoremap <leader>{ :set nopaste<CR>

"-------
" Splits
"-------
" Bindings for easy split nav (Control and Command)
nnoremap <leader>h <C-w>h
nnoremap <leader>j <C-w>j
nnoremap <leader>k <C-w>k
nnoremap <leader>l <C-w>l

" Swap Splits
noremap <C-t> <C-w><C-r>

function! MarkWindowSwap()
    let g:markedWinNum = winnr()
endfunction

function! DoWindowSwap()
    "Mark destination
    let curNum = winnr()
    let curBuf = bufnr( "%" )
    exe g:markedWinNum . "wincmd w"
    "Switch to source and shuffle dest->source
    let markedBuf = bufnr( "%" )
    "Hide and open so that we aren't prompted and keep history
    exe 'hide buf' curBuf
    "Switch to dest and shuffle source->dest
    exe curNum . "wincmd w"
    "Hide and open so that we aren't prompted and keep history
    exe 'hide buf' markedBuf 
endfunction

" Swap window buffers.
function! SwapWindowBuffers()
    if !exists("g:markedWinNum")
        " set window marked for swap
        let g:markedWinNum = winnr()
        :echo "window marked for swap"
    else
        " mark destination
        let curNum = winnr()
        let curBuf = bufnr( "%" )
        if g:markedWinNum == curNum
            :echo "window unmarked for swap"
        else
            exe g:markedWinNum . "wincmd w"
            " switch to source and shuffle dest->source
            let markedBuf = bufnr( "%" )
            " hide and open so that we aren't prompted and keep history
            exe 'hide buf' curBuf
            " switch to dest and shuffle source->dest
            exe curNum . "wincmd w"
            " hide and open so that we aren't prompted and keep history
            exe 'hide buf' markedBuf
            :echo "windows swapped"
        endif
        " unset window marked for swap
        unlet g:markedWinNum
    endif
endfunction

"noremap <leader><C-s> :call SwapWindowBuffers()<CR>
"nmap <silent> <leader>mw :call MarkWindowSwap()<CR>
"nmap <silent> <leader>pw :call DoWindowSwap()<CR>

" Pane resizing
nnoremap <silent> - :exe "resize " . (winheight(0) * 3/2)<CR>
nnoremap <silent> = :exe "resize " . (winheight(0) * 2/3)<CR>
nnoremap <silent> _ :exe "vertical resize " . (winwidth(0) * 3/2)<CR>
nnoremap <silent> + :exe "vertical resize " . (winwidth(0) * 2/3)<CR>
nnoremap <leader>- :res +5<CR>
nnoremap <leader>= :res -5<CR>
nnoremap <leader>] :vertical resize +5<CR>
nnoremap <leader>[ :vertical resize -5<CR>

" Split Panes
noremap <leader>v <C-w>v<C-w>l    " create new vsplit, and switch to it.
noremap <leader>s <C-w>s<C-w>j    " create new hsplit, and switch to it.

" Saving and Quiting
noremap <leader>w :w<CR>    " Save
noremap <leader>e :wq<CR>   " Save and quit
noremap <leader>q :q<CR>    "  Quit
noremap <leader>Q :qa!<CR>  " Force Quit All

" Go back and center screen
noremap <C-o> <C-o>zz

" Redo
noremap <leader>u <C-r> 

" Modify .vimrc
noremap <leader>t :vsplit<CR>:e ~/.vimrc<CR>           " Edit .vimrc
noremap <leader>T :source ~/.vimrc<CR>                 " Apply .vimrc

" Turns off search highlight
noremap <leader>; :nohlsearch<CR>

" Replace text
noremap <leader>R :%s//g<left><left>

"-----------
" Formatting
"-----------
" Format selection
noremap <leader><F2> =

" Format whole file
noremap <leader><F1> gmzgg=G`z

"--------------
" Save location
"--------------
" Return to last edit position when opening files (You want this!)
autocmd BufReadPost *
            \ if line("'\"") > 0 && line("'\"") <= line("$") |
            \   exe "normal! g`\"" |
            \ endif
" Remember info about open buffers on close
set viminfo^=%

"====================
" Standard Settings
"====================

" Syntax highlighting
syntax on

" Show line numbers
set number

" Disable errorbells
set visualbell
set t_vb=

" History Count
set history=1000

" Visual Menu for file completion
set wildmenu
set wildmode=list:longest

" Tabs and Indents
set tabstop=4
set shiftwidth=4
set softtabstop=4
set backspace=2
set expandtab   " use spaces instead of tabs.
set smarttab    " let's tab key insert 'tab stops', and bksp deletes tabs.
set shiftround  " tab / shifting moves to closest tabstop.
set autoindent  " Match indents on new lines.
set cindent     " Intellegently dedent / indent new lines based on rules.

" Show Whitespace
nmap <leader>\ :set list!<CR>
set list
set listchars=eol:¬,tab:▸\ ,trail:·,nbsp:⎵

" We have VCS -- we don't need this stuff.
set nobackup " We have vcs, we don't need backups.
set nowritebackup " We have vcs, we don't need backups.
set noswapfile " They're just annoying. Who likes them?

" Don't nag me when hiding buffers
set hidden " allow me to have buffers with unsaved changes.
set autoread " when a file has changed on disk, just load it. Don't ask.

" Make search more sane
set ignorecase  " case insensitive search
set smartcase   " If there are uppercase letters, become case-sensitive.
set incsearch   " live incremental searching
set showmatch   " live match highlighting
set hlsearch    " highlight matches
set gdefault    " use the `g` flag by default.

" Set Color Mode
set t_Co=256

" Allow the cursor to go anywhere in visual block mode.
set virtualedit+=block

" Set relative line numbers in visual mode.
nnoremap <silent> v :<C-u>set rnu<CR>v
nnoremap <silent> V :<C-u>set rnu<CR>V
nnoremap <silent> <leader>V :<C-u>set rnu<CR><C-v>
nnoremap <silent> gv :<C-u>set rnu<CR>gv
vnoremap <Esc> <Esc>:set nornu nu<CR>
:autocmd CursorMoved * if mode() !~# "[vV\<C-v>]" | set nornu nu | endif
nnoremap <leader>N :set rnu<CR>

"-------------------------
" Visual Diff Color Scheme
"-------------------------
if &diff
    colorscheme diffColorScheme
    noremap <leader>q :qa!<CR>   " Force Quit All
    au BufReadPost * :goto 1
endif


"================
" PLUGIN MAPPINGS
"================

" Vundle
noremap <leader>al :PluginList<CR>      " pulls up plugins
noremap <leader>ai :PluginInstall<CR>   " installs plugins
noremap <leader>ac :PluginClean<CR>     " cleans plugins
noremap <leader>as :PluginSearch!<CR>   " searches plugins

" Rainbow Parens
let g:rainbow_active = 1

" NERD Tree
function! NERDTreeQuit()
    redir => buffersoutput
    silent buffers
    redir END
    "   1BufNo  2Mods.     3File           4LineNo
    let pattern = '^\s*\(\d\+\)\(.....\) "\(.*\)"\s\+line \(\d\+\)$'
    let windowfound = 0

    for bline in split(buffersoutput, "\n")
        let m = matchlist(bline, pattern)

        if (len(m) > 0)
            if (m[2] =~ '..a..')
                let windowfound = 1
            endif
        endif
    endfor

    if (!windowfound)
        quitall
    endif
endfunction
autocmd WinEnter * call NERDTreeQuit()
noremap <leader>i :NERDTreeFind<CR>
let g:NERDTreeQuitOnOpen = 1

" Airline
set laststatus=2
let g:airline_theme="badwolf"
let g:airline#extensions#whitespace#enabled=0

" Sneak
let g:sneak#streak = 1
let g:sneak#s_next = 1
nmap s H<Plug>Sneak_s
nmap S L<Plug>Sneak_S
let g:sneak#use_ic_scs = 1

" Tagbar
nmap <leader>p :TagbarToggle<CR>
let g:tagbar_autofocus = 1
let g:tagbar_autoclose = 1

" Syntastic
"set statusline+=%#warningmsg#
"set statusline+=%{SyntasticStatuslineFlag()}
"set statusline+=%*
"let g:syntastic_always_populate_loc_list = 1
"let g:syntastic_auto_loc_list = 1
"let g:syntastic_check_on_open = 1
"let g:syntastic_check_on_wq = 0

" Jedi
"let g:jedi#use_splits_not_buffers = "top"
let g:jedi#use_tabs_not_buffers = 1
let g:jedi#show_call_signatures = 0

" Supertab
let g:SuperTabDefaultCompletionType = "<c-n>"
let g:SuperTabContextDefaultCompletionType = "<c-n>"

" Reprocessed
let g:reprocessed_map_keys = 0
"nnoremap <leader>1 :RunCurrentSketch<cr>
"nnoremap <leader>2 :PresentCurrentSketch<cr>

" Ags
noremap <leader>x :Ags -C 0 <cword> /fierce/<CR>
noremap <leader>X :Ags -C 3 <cword> /fierce/<CR>
noremap <leader>a : /fierce/<C-b>Ags -C 0 
noremap <leader>z :AgsQuit<CR>
noremap <leader>Z :AgsLast<CR>
" Let 'ot' open in a new tab
autocmd FileType agsv nnoremap <buffer> ot
  \ :exec 'tab split ' . ags#filePath(line('.'))<CR>
" Make it play nice with rainbow parens extension
let g:rainbow_conf = {
      \ 'separately' : { 'agsv' : 0, 'agse' : 0 }
      \ }

" Ag
"noremap <leader>x :Ag <cword>
"noremap <leader>a : ~/repos/website/counsyl/product<C-b>Ag 
"noremap <leader>X : ~/repos/website/counsyl/product<C-b>Ag "class "<left>
"let g:ag_qhandler="botright copen 30"
"let g:ag_working_path_mode="r"

" YouCompleteMe
let g:ycm_python_binary_path = '/usr/local/bin/python3'
let g:ycm_autoclose_preview_window_after_completion = 1
let g:ycm_add_preview_to_completeopt = 0
set completeopt-=preview
noremap <leader>d :YcmCompleter GoTo<CR>
noremap <leader>D :YcmCompleter GoToDeclaration<CR>
noremap <leader>n :YcmCompleter GoToReferences<CR>
"python with virtualenv support
py << EOF
import os
import sys
if 'VIRTUAL_ENV' in os.environ:
  project_base_dir = os.environ['VIRTUAL_ENV']
  activate_this = os.path.join(project_base_dir, 'bin/activate_this.py')
  execfile(activate_this, dict(__file__=activate_this))
EOF


" Ctrl-P
let g:ctrlp_map = ''
let g:ctrlp_cmd = 'CtrlP'
let g:ctrlp_by_filename = 0
let g:ctrlp_working_path_mode = 'ra'
let g:ctrlp_clear_cache_on_exit = 0
let g:ctrlp_max_height = 20
let g:ctrlp_prompt_mappings = {
  \ 'AcceptSelection("e")': [';'],
  \ 'AcceptSelection("t")': ['<cr>', '<c-m>'],
  \ 'AcceptSelection("v")': ["'", '<c-v>'],
  \ }
"let g:ctrlp_user_command = 'ag %s -l --nocolor --hidden -g ""'
"let g:ctrlp_user_command = 'ag %s -l --nocolor --ignore "\.git$\|\.hg$\|\.oh-my-zsh$\|\.svn$" -g ""'

"let g:ctrlp_custom_ignore = '.git'
let g:ctrlp_custom_ignore = '\v[\/]\.(git|hg|svn)$'
let g:ctrlp_custom_ignore = {
  \ 'dir':  '\v[\/]\.(git|hg|svn)$',
  \ 'file': '\v\.(exe|so|dll)$',
  \ 'link': 'some_bad_symbolic_links',
  \ }
noremap <leader>f :CtrlP<CR>
noremap <leader>m :CtrlPMRU<CR>
"noremap <leader>b :CtrlPBuffer<CR>

"==============================================================================
" After the fact settings
"==============================================================================

"-------------------------
" Save Session
"nnoremap <leader>S :mksession! ~/repos/config/vim_sessions/COM-.vim<left><left><left><left>

"-------------------------
" Color Column at 80 chars¬
"-------------------------
set colorcolumn=+1
let &colorcolumn=join(range(80,999),",")
highlight ColorColumn ctermbg=235

"--------------
" Line wrapping
"--------------
set nowrap
set textwidth=0
"set wrap
"set textwidth=80

"------------
" Cursor Line¬
"------------
set cursorline
hi CursorLine cterm=None ctermbg=237
hi Folded ctermfg=18 ctermbg=243

hi CursorLineNR cterm=bold
augroup CLNRSet
    autocmd! ColorScheme * hi CursorLineNR cterm=bold
augroup END

"-----------------"
" Add No PEP8
"-----------------"
"nnoremap <leader>nn A<space><space># nopep8<ESC>^

"-----------------"
" Set Breakpoint
"-----------------"
nnoremap <leader>sb oimport ipdb; ipdb.set_trace()<ESC>:w<CR>

" Move split into a new tab and then go back (Depends on <leader>TAB mapping)
map <leader>0 :tabe %<CR><leader><TAB>:q<CR>

" Paste at the end of the line
nmap <leader>P A<ESC>p
nmap <leader>A A<space><ESC>p

" Navigate to specific classes
"nnoremap <leader>gc :vsplit<CR><C-w>l:edit ~/nfs-share/website/counsyl/product/my/models/__init__.py<CR> /class CustomerProfile(<CR>:noh<CR>zt2k2j
"nnoremap <leader>gp :vsplit<CR><C-w>l:edit ~/nfs-share/website/counsyl/product/my/models/__init__.py<CR> /class Person(<CR>:noh<CR>zt2k2j
"nnoremap <leader>ga :vsplit<CR><C-w>l:edit ~/nfs-share/website/counsyl/product/api/models.py<CR> /class APIOrder(<CR>:noh<CR>zt2k2j
"nnoremap <leader>gok :vsplit<CR><C-w>l:edit ~/nfs-share/website/counsyl/product/order/models/__init__.py<CR> /class OrderKit(<CR>:noh<CR>zt2k2j

"""""""""""""""""
" Tags
"""""""""""""""""
:nnoremap <leader>tj :tj 
set tw=0

"-------------------------
" Move tabs left and right
"-------------------------
nnoremap <C-f> :call ShiftTabLeft()<CR>
nnoremap <C-g> :call ShiftTabRight()<CR>
noremap <C-b> gT
noremap <C-n> gt

"-----------------------
" No idea what this does
"-----------------------
autocmd FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o
